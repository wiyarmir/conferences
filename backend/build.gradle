import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group = 'es.guillermoorellana.keynotedex'
version = keynotedexVersion

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$Versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$Versions.kotlin"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "io.ktor.server.netty.EngineMain"

sourceCompatibility = 1.8

compileKotlin.kotlinOptions.jvmTarget = '1.8'
compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

sourceSets {
    release {
        kotlin {
            srcDirs += sourceSets.main.kotlin.srcDirs
        }
        resources {
            srcDirs += sourceSets.main.resources.srcDirs
        }

        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    releaseCompile.extendsFrom compile
}

dependencies {
    compile project(":common-jvm")

    compile Libs.kotlinStdlib
    compile Libs.kotlinReflect

    compile Libs.ktorLocations
    compile Libs.ktorGson
    compile Libs.ktorFreemaker
    compile Libs.ktorHtmlBuilder
    compile Libs.ktorServerNetty
    compile Libs.ktorClientApache

    compile Libs.ehcache

    compile(Libs.squashH2){
        exclude group: "org.jetbrains.kotlin"
    }

    testCompile Libs.kotlinTestJunit
    testCompile Libs.ktorServerTestHost
    testCompile Libs.jsoup

    compile Libs.gson
    compile Libs.logbackClassic
    compile Libs.fuel
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task releaseJar(type: ShadowJar) {
    manifest {
        attributes "Main-Class": mainClassName
    }
    classifier = 'release'
    from([sourceSets.release.output, sourceSets.main.output])
    configurations = [project.configurations.compile]
}

task release() {
    dependsOn('clean')
    dependsOn('releaseJar')
}

releaseJar.mustRunAfter clean

task copyWebBundle(type: Copy) {
    dependsOn(':web:build')
    from('../web/build/bundle')
    into('build/resources/release')
}

processReleaseResources.dependsOn copyWebBundle
