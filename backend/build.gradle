import java.nio.file.Paths

group = 'es.guillermoorellana.keynotedex'
version = keynotedexVersion

apply plugin: "kotlin"
apply plugin: 'kotlinx-serialization'
apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "io.ktor.server.netty.EngineMain"

sourceCompatibility = 1.8

compileKotlin.kotlinOptions.jvmTarget = '1.8'
compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

dependencies {
    implementation project(":common")

    implementation Libs.kotlinStdlib
    implementation Libs.kotlinReflect

    implementation Libs.kotlinxSerializationRuntime

    implementation Libs.okhttp

    implementation Libs.ktorLocations
    implementation Libs.ktorFreemaker
    implementation Libs.ktorHtmlBuilder
    implementation Libs.ktorServerNetty
    implementation Libs.ktorClientOkHttp
    implementation Libs.ktorAuthJwt

    implementation Libs.ehcache

    implementation(Libs.squashH2) {
        exclude group: "org.jetbrains.kotlin"
    }

    implementation Libs.logbackClassic
    implementation Libs.fuel

    implementation Libs.kaml

    testImplementation Libs.kotlinTestJunit
    testImplementation Libs.ktorServerTestHost
    testImplementation Libs.jsoup
    testImplementation Libs.mockitoKotlin
    testImplementation Libs.mockWebServer

    implementation "com.squareup.okhttp3:logging-interceptor:4.0.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ['-Xuse-experimental=kotlin.Experimental']
}

shadowJar {
    archiveClassifier.set('release')
    from({ Paths.get(project(':web').buildDir.path, 'bundle') })
}

tasks['shadowJar'].mustRunAfter clean
tasks['processResources'].mustRunAfter ':web:bundle'

task release() {
    dependsOn('clean')
    dependsOn(':web:bundle')
    dependsOn('shadowJar')
}
