import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group = 'es.guillermoorellana.keynotedex'
version = keynotedexVersion

apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "io.ktor.server.netty.EngineMain"

sourceCompatibility = 1.8

compileKotlin.kotlinOptions.jvmTarget = '1.8'
compileTestKotlin.kotlinOptions.jvmTarget = '1.8'

sourceSets {
    release {
        kotlin {
            srcDirs += sourceSets.main.kotlin.srcDirs
        }
        resources {
            srcDirs += sourceSets.main.resources.srcDirs
        }

        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    implementation project(":common")

    implementation Libs.kotlinStdlib
    implementation Libs.kotlinReflect

    implementation Libs.kotlinxSerializationRuntime

    implementation Libs.ktorLocations
    implementation Libs.ktorFreemaker
    implementation Libs.ktorHtmlBuilder
    implementation Libs.ktorServerNetty
    implementation Libs.ktorClientApache
    implementation Libs.ktorAuthJwt

    implementation Libs.ehcache

    implementation(Libs.squashH2) {
        exclude group: "org.jetbrains.kotlin"
    }

    testImplementation Libs.kotlinTestJunit
    testImplementation Libs.ktorServerTestHost
    testImplementation Libs.jsoup
    testImplementation Libs.mockitoKotlin

    implementation Libs.logbackClassic
    implementation Libs.fuel
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = 'release'
}

task release() {
    dependsOn('clean')
    dependsOn('copyWebBundle')
    dependsOn('shadowJar')
}

task copyWebBundle(type: Copy) {
    dependsOn(':web:build')
    from('../web/build/bundle')
    into('build/resources/release')
}

tasks['shadowJar'].dependsOn copyWebBundle
tasks['shadowJar'].mustRunAfter clean
