group = 'es.guillermoorellana.keynotedex.web'
version = '0.0.1'

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    jcenter()
}

dependencies {
    compile project(":common-js")

    compile Libs.kotlinStdlibJs
    testCompile Libs.kotlinTestJs

    compile Libs.kotlinxHtmlJs
    compile Libs.kotlinxCoroutinesCoreJs

    compile Libs.kotlinExtensions
    compile Libs.kotlinReact
    compile Libs.kotlinReactDom
}

kotlinFrontend {

    downloadNodeJsVersion = Versions.node

    sourceMaps = false

    npm {
        replaceVersion "kotlinx-html-js", Versions.kotlin
        replaceVersion "kotlinx-html-shared", Versions.kotlin
        replaceVersion "kotlin-js-library", Versions.kotlin

        dependency "react", "16.3.1"
        dependency "react-dom"
        dependency "react-router"
        dependency "react-router-dom"
        dependency "react-markdown"
        dependency "jquery"

        devDependency "babel-loader", "7"
        devDependency "babel-core", "6"
        devDependency "css-loader"
        devDependency "karma"
        devDependency "style-loader"
        devDependency "source-map-loader"
    }

    webpackBundle {
        publicPath = "/frontend/"
        port = 8080
        proxyUrl = "http://localhost:9090/"
        sourceMapEnabled = true
        stats = "errors-only"
    }
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
